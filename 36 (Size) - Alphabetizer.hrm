-- 36 - Alphabetizer
-- Size Challenge: 39/39
-- The INBOX contains exactly two words.
-- Determine which word comes first,
-- if you were to order them alphabetically.
-- Send only that word to the OUTBOX.

-- [20]: first word current index
-- [21]: second word current index
-- [22]: second word starting index
-- [23]: zero (also temporarily a boolean)
-- [24]: output current index

main:
COPYFROM 23
COPYTO 21
BUMPUP 23

storeWord:
INBOX
COPYTO [21]
JUMPZ wordIsFinished
BUMPUP 21
JUMP storeWord

wordIsFinished:
COPYFROM 23
JUMPZ compareWords
BUMPUP 21
COPYTO 22
BUMPDN 23
JUMP storeWord

compareWords:
COPYFROM 22
COPYTO 21
COPYFROM 23
COPYTO 20

loop:
COPYFROM [20]
JUMPZ outputFirstWord
COPYFROM [21]
JUMPZ outputSecondWord

COPYFROM [20]
SUB [21]
JUMPZ nextIteration
JUMPN outputFirstWord

outputSecondWord:
COPYFROM 22
COPYTO 24
JUMP outputWord

outputFirstWord:
COPYFROM 23
COPYTO 24

outputWord:
COPYFROM [24]
JUMPZ main
OUTBOX
BUMPUP 24
JUMP outputWord

nextIteration:
BUMPUP 20
BUMPUP 21
JUMP loop